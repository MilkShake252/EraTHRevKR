;애무계 리액션 커맨드군
;────────────────────────────────────
;070, 가게해줘!
;────────────────────────────────────
@COM70
;이탈SOURCE 감소
TIMES SOURCE:MASTER:이탈 , 0.70
;트라우마화 감속
TIMES SOURCE:MASTER:트라우마 , 0.80
;굴종, 달성, 열락SOURCE 증가
SOURCE:MASTER:굴종 += 50 + (MARK:MASTER:쾌락각인 + 1) * (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) * CFLAG:MASTER:조교레벨 / 200
SOURCE:MASTER:달성 += 100 + (CFLAG:MASTER:조교레벨 + 5) * (SOURCE:MASTER:쾌Ｃ + SOURCE:MASTER:쾌Ｖ + SOURCE:MASTER:쾌Ａ + SOURCE:MASTER:쾌Ｂ) / 100
SOURCE:MASTER:열락 += 100 + SOURCE:MASTER:쾌Ｃ / 25 + SOURCE:MASTER:쾌Ｖ / 25 + SOURCE:MASTER:쾌Ａ / 25 + SOURCE:MASTER:쾌Ｂ / 25
;보정치 초기화
VARSET LOCAL
;소질보정치 설정
LOCAL = (CFLAG:호의 / 500) + TALENT:색정적 - TALENT:청초 + TALENT:헌신적 - TALENT:수동적
LOCAL = LIMIT(LOCAL, 1, 5)
;가학보정치 설정
;3<=(LOCAL:1)<=8
SIF TALENT:새드
	LOCAL:1 += 1
SIF TALENT:심술궂음
	LOCAL:1 += 1
IF ABL:가학 >= ABL:애무
	LOCAL:1 += 1
ELSE
	LOCAL:1 -= 1
ENDIF
SIF TALENT:마음씨착한
	LOCAL:1 -= 1
LOCAL:1 += 5
;소질에 의해애태움종료
IF RAND:LOCAL > 0
	TCVAR:MASTER:애태움도 = 1
	;가학보정에 의해애태움연장
	IF RAND:(LOCAL:1) >= RAND:5
		;기교에 의해 기간 결정
		LOCAL:2 = ABL:기교 / 5 + 2
		SIF LOCAL:2 > 4
			LOCAL:2 = 4
		TCVAR:MASTER:애태움도 += LOCAL:2
	ENDIF
ENDIF
IF TCVAR:MASTER:애태움도 == 1
	;파생:가게해줘 준다
	TFLAG:REACT파생 = 1
ELSEIF TCVAR:MASTER:애태움도 > 1
	;파생:가게해줘 주지 않는다
	TFLAG:REACT파생 = 2
ELSE
	;파생:「흐음」
	TFLAG:REACT파생 = 0
ENDIF
;가게해줘상태에 이행
TCVAR:MASTER:가게해줘거동제어용 = 1
;COM 분류
CALL SET_COMGRO("받아들인다")
;COM 인상
CALL SET_COMIMP("좋은인상")
;의존도변화
CALL ADD_DEPEND(+10, +6)
RETURN 1

;────────────────────────────────────
;071, 키스한다
;────────────────────────────────────
@COM71
;달성, 열락 SOURCE증가
SOURCE:MASTER:애정 += 35 + CFLAG:M호의 / 40 + GET_ABL(MASTER, "봉사정신") * 3
SOURCE:MASTER:달성 += (CFLAG:M호의 + 500) * (GET_ABL(MASTER, "봉사정신") + 10) * (GET_ABL(MASTER, "기교") + 10) / 10000
SOURCE:MASTER:열락 += (CFLAG:M호의 + 100) * (GET_TRAINLV(TARGET) + 10) / 50
;COM분류
CALL SET_COMGRO("적극적으로 한다")
;COM인상
CALL SET_COMIMP("좋은인상")
;참는다 플래그 해소
TCVAR:MASTER:참다 = 0
;ACT가 키스가 아닐 시, 더러움 이동
IF !IS_NOWACTNAME("키스")
	IF IS_NOWACTNAME("발핥기강제")
		;더러움 이동
		CALL MOVE_STAIN("발", TARGET, "입", MASTER)
	ELSE
		;더러움 이동
		CALL MOVE_STAIN("입", TARGET, "입", MASTER)
	ENDIF
ENDIF
;의존도 변화
;원래는 조교자가 5고 조교 대상이 10이었는데, 어차피 둘 다 애정경험 있을 정도면 조교자도 이미 갈데까지 빠져버린 상태라서 걍 10, 10으로 고침
CALL ADD_DEPEND(+10, +10)
RETURN 1
;키스 경험 추가는 @EXP_CHECK에서 실시

